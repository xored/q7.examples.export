--- Q7 testcase ---
Format-Version: 1.0
Q7-vendor: www.xored.com/q7
Contexts: _cKLWQKRHEeOyO_Oxb5ZR5g,_CCKhAKRGEeOyO_Oxb5ZR5g
Element-Name: Export Test
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _g4-AoKRHEeOyO_Oxb5ZR5g
Q7-Runtime-Version: 1.3.10.201312200736
Save-Time: 3/5/14 4:31 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-view "Project Explorer" | get-tree | select sampleJavaProject | get-menu "Export..." | click
with [get-window Export] {
    get-tree | select "General/Preferences"
    get-button "Next >" | click
}

with [get-window "Export Preferences"] {
	get-button "Export all" | check
	
	// The destination file will be inside AUT's workspace project 'exportedFiles'
	// An associated workspace context makes sure this project exists and empty
	set-dialog-result File "platform:/resource/exportedFiles/myprefs.epf"
    get-button "Browse..." | click
    get-button "Overwrite existing files without warning" | check
    get-button Finish | click
}

// Once export is finished, we need to check export results
with [get-view "Project Explorer" | get-tree] {
	// At first, refresing this project -- as export operations
	// don't deal with workspace resources and work with file system,
	// project won't be refreshed automatically.
	// However, after manual refresh it should contain all files
	// created during export
    select exportedFiles | get-menu Refresh | click
    
    // Now selecting an exported file and opening it with a text editor
    select "exportedFiles/myprefs.epf" | select "exportedFiles/myprefs.epf" | get-menu "Open With/Text Editor" | click
}

// Finally, validating editor contents
get-editor "myprefs.epf" | get-text-viewer | get-property text | contains "file_export_version=3.0" | verify-true
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
